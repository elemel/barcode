    main, cal; Run main
    hcf; Halt program

main: .exit_code = 0, .argv = 1, .argc = 2
    0
.loop:
    dup, ldp + .argc, sub, beq + .break; Break after last argument
    dup, beq + .first; Skip space before first argument
    " ", stdout, put; Write space to standard output
.first:
    dup, ldp + .argv, add, ldm; Load argument
    stdout, print, cal, top - 2; Print argument to standard output
    inc, bal + .loop; Next argument
.break:
    "\n", stdout, put; Write newline to standard output
    ret

; Print string to stream
print: .stream = 0, .string = 1
    ldp + .string
.loop:
    dup, ldm; Load character
    dup, beq + .break; Break on null character
    ldp + .stream, put; Write character to stream
    inc, bal + .loop; Next character
.break:
    ret
