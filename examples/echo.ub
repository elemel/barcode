    main, cal; Run main
    hcf; Halt program

main: main.exit_code = 0, main.argv = 1, main.argc = 2
    0
main.loop:
    dup, ldp + main.argc, sub, beq + main.break; Break after last argument
    dup, beq + main.first; Skip space before first argument
    " ", stdout, put; Write space to standard output
main.first:
    dup, ldp + main.argv, add, ldm; Load argument
    stdout, print, cal, top - 2; Print argument to standard output
    inc, bal + main.loop; Next argument
main.break:
    "\n", stdout, put; Write newline to standard output
    ret

; Print string to stream
print: print.stream = 0, print.string = 1
    ldp + print.string
print.loop:
    dup, ldm; Load character
    dup, beq + print.break; Break on null character
    ldp + print.stream, put; Write character to stream
    inc, bal + print.loop; Next character
print.break:
    ret
