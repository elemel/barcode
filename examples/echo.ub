    clo + main; Run main
    hcf; Halt program

; [argc, argv] -> [exit_code]
main: .argc = 0, .argv = 1
    ent + 2, stl + .argc, stl + .argv
    0
.loop:
    dup, ldl + .argc, sub, beq + .break; Break after last argument
    dup, beq + .first; Skip space before first argument
    " ", stdout, put; Write space to standard output
.first:
    dup, ldl + .argv, add, ldm; Load argument
    stdout, clo + print; Print argument to standard output
    inc, bal + .loop; Next argument
.break:
    top - 1
    "\n", stdout, put; Write newline to standard output
    0, ret + 2

; [stream, string] -> []
print: .stream = 0
    ent + 1
    stl + .stream
.loop:
    dup, ldm; Load character
    dup, beq + .break; Break on null character
    ldl + .stream, put; Write character to stream
    inc, bal + .loop; Next character
.break:
    top - 2
    ret + 1
